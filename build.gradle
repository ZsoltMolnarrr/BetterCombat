buildscript {
    configurations.classpath {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
    id "org.cadixdev.licenser" version "0.6.1"
    id "io.github.juuxel.loom-quiltflower" version "1.7.+" apply false
    id "me.shedaniel.unified-publishing" version "0.1.+" apply false
    id "com.modrinth.minotaur" version "2.4.3"
    id "maven-publish"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    archivesBaseName = rootProject.archives_base_name + "-${project.name}"
    version = rootProject.mod_version + "+1.19"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.architectury.minecraft}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release.set 17
    }

    java {
        withSourcesJar()
    }
}

ext.changelog_last_section = { ->
    return new File("CHANGELOG.md").text.split('\n# ')[0]
}